mutable Scene scene( "Main" );

scene.on_open = Function <Nil> () {
    # the music.play function is in this case playing the music file located at
    # "assets/captain_alexi_theme.ogg".  All music files will be exported into the
    # local namespace as long as they have the supported extension ".ogg"
    
    music.play( assets.captain_alexi_theme );
    
    # Note: We don't support MP3's, its better to just reconvert them to ogg
};

scene.on_update = Function <Boolean> () {

    if( input.press( escape ) )
    {
        return false;
    }
    
    if( input.press( up ) )
    {
        # the sound.play function will play any sound files in the local namespace
        # loaded from ".wav" files in the project folders
    
        sound.play(assets.bleep);
    }
    if( input.press( down ) )
    {
        # You can specify a second volume parameter on sound.play
        sound.play(assets.bleep, 0.2);
    }
    if( input.press( left ) )
    {
        Integer clipChannel = sound.play(assets.bleep);
        # sound.play returns an integer for which sound-card channel
        # the sounds is playing on.  You can use this sound channel to
        # change the panning of the played clip.  Currently only 2.0 surround is supported
        sound.set_panning( clipChannel, -1 );
    }
    if( input.press( right ) )
    {
        Integer clipChannel = sound.play(assets.bleep);
        sound.set_panning( clipChannel, 1 );
    }
    
    return true;
};


scene.on_2d = Function <Nil> () {  
    graphics.box( IntegerVector2D(0,0), graphics.get_resolution(), green );

    Integer screenCenter(graphics.get_resolution().x/2);
    graphics.print_centered( screenCenter, 8, "Press arrow keys to play sound", white );
};
