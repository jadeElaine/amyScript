mutable Boolean quitTrigger( false );
String badTextResponse( "That made no sense whatsoever." );

mutable Dictionary items {
    pants : Dictionary {
        defaultResponse : "The pants give you the power of sexy.. and crimefighting!"
    },
    key : Dictionary {
        defaultResponse : "There isn't anything here to unlock, but you practice turning the KEY anyway."
    }
};

Function buildHelperString <String> ( links : Array ) {
    mutable String result = " [";

    Array itemKeys( items.keys() );
    mutable Boolean first( true );
    for( mutable Integer i=0; i<itemKeys.size(); i+=1 )
    {
        if( first ) { first = false; } else { result += ", "; }
        result += "USE " + upper(itemKeys[i]);
    }
    for( mutable Integer i=0; i<links.size(); i+=1 )
    {
        if( first ) { first = false; } else { result += ", "; }
        result += "GO " + upper(links[i]);
    }
    
    result += "]>";
    return result;
};

# # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # #
#  Main io function to determine what to do from the player's input #
# # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # #
Function processInput <String> ( hostState : FSMState ) {
    console_print( buildHelperString( hostState.links ) );
    String entry( lower( console_input() ) );
    
    # quit game
    if( entry == "quit" )
    { 
        quitTrigger = true; 
        return "";
    }
    
    # transition to other states with "go"
    for( mutable Integer i=0; i<hostState.links.size(); i+=1 )
    {
        if( entry == "go " + hostState.links[i] )
        {
            return hostState.links[i];
        }
    }
    
    # use items in area
    Array itemKeys( items.keys() );
    for( mutable Integer i=0; i<itemKeys.size(); i+=1 )
    {
        if( entry == "use " + itemKeys[i] )
        {
            if( hostState.contains_key( "on_" + itemKeys[i] ) )
            {
                return hostState["on_" + itemKeys[i]]();
            }
            else
            {
                console_print( "\n   " + items[itemKeys[i]].defaultResponse );
                return "";
            }
        }
    }
    
    console_print( "You typed " + entry + "\n\n" );
    return "";
};

# # # # # # # # # # # # # # # # # #
#  Definitions of all game states #
# # # # # # # # # # # # # # # # # #
mutable FSM stateMachine {
    pole : { 
        on_update : Function <String> ( timeStep : Real ) { 
            console_print("\n\n   You stand atop of a telephone POLE. The wind buffets your hair about\n");
            console_print("epically. You look really awesome and stuff, but maybe you should think about\n");
            console_print("hopping down to the STREET.\n\n");
            return processInput( this );
        },
        
        links : Array <String> { street }
    },
    street : {
        on_update : Function <String> ( timeStep : Real ) {
            console_print("\n\n   The STREET is mostly deserted, since everyone is inside hiding from Momo and\n");
            console_print("his Mechasaurs.\n");
            console_print("   The old town SQUARE is nearby, or there's always epic telephone POLE\n");
            console_print("standing to be done.\n\n");
            return processInput( this );
        },
        links : Array <String> { square, pole }
    },
    square : {
        on_update : Function <String> ( timeStep : Real ) {
            console_print("\n\n   This is the old town SQUARE of what is now the city of Graradragonsville.\n");
            console_print("A statue of Igjhklyhl the Valiant stands at its center, with a young Failagon\n");
            console_print("whelp at his feet.\n");
            console_print("   There are four areas on each side of the SQUARE: the BANK, the PRISON, the\n");
            console_print("DRIVETHRU and the STREET.\n\n");
            return processInput( this );
        },
        links : Array <String> { bank, prison, drivethru, street }
    },
    bank : {
        on_update : Function <String> ( timeStep : Real ) {
            console_print("\n\n   Holy fuck a bank and it's getting held up by a mecha-dactyl arrrgh!\n");
            console_print("The mechadactyl can kill you and stuff, and we get a bad ending if you are not\nteh smart.\n\n");
            return processInput( this );
        },
        links : Array <String> { square }
    },
    prison : {
        on_update : Function <String> ( timeStep : Real ) {
            console_print("\n\n   You stand inside the Graradragonsville PRISON. Some of the city's most\n");
            console_print("notorious criminals are held here. Most of them have you to thank for it.\n\n");
            return processInput( this );
        },
        on_key : Function <String> () {
            console_print("\n\n   Dude.  that is a fucking terrible idea...\n\n");
            return "";
        },
        links : Array <String> { square }
    },
    drivethru : {
        on_update : Function <String> ( timeStep : Real ) {
            console_print("\n\n   Next door is the CLEANERS. You can also return to the SQUARE.\n\n");
            return processInput( this );
        },
        links : Array <String> { square, cleaners }
    },
    cleaners : {
        on_update : Function <String> ( timeStep : Real ) {
            console_print("\n\n   You can enter the cleaners, but need a KEY to do... something.\n\n");
            return processInput( this );
        },
        on_key : Function <String> () {
            console_print("\n\n   You unlock the secret victory box...\n\n");
            console_print("AND EVERYTHING IS FIXED AND YOU WIN!\n\n");
            console_input();
            
            quitTrigger = true;
            return "";
        },
        links : Array <String> { drivethru }
    }
};

# # # # # # # # # # # # # # # # # # #
#  Game introduction and main loop  #
# # # # # # # # # # # # # # # # # # #
Function on_load <Nil> () {
    console_clear();
    console_print("   The city of Graradragonsville IS UNDER ATTACK!   ...AGAIN!\n");
    console_print("Ever since that asshole Momo escaped from the local prison, he has been\n");
    console_print("terrorizing the place with GIANT FUCKING MECHANICAL DINOSAURS! :0\n\n");
    console_print("   The local law enforcement squad has tried and failed to contain the\n");
    console_print("situation, leaving once-great landmarks laid to waste and the city itself to\n");
    console_print("the mercy of the merciless mecha-repitilian mercenaries..\n\n");
    console_print("   Only a true hero could possibly absolve this situation now. Only a very\n");
    console_print("heroic, very specific hero, which is YOU! That most vigilant and capable\n");
    console_print("of caped vigilantes...\n\n");
    console_print("   PRYNCESS PANTS!\n\n");
    console_print("------------------------------------------------------");

    stateMachine.change(pole);
};

Function on_update <Boolean> () {
    stateMachine.force_update();
    return !quitTrigger;
};
